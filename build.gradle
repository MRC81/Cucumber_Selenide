plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'tst.investing'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    selenideVersion = '7.2.3'
    junitVersion = "5.10.2"
    junitPlatformVersion = "1.10.2"
    cucumberVersion = "7.16.1"
    allureVersion= "2.26.0"
    slf4jVersion = "2.0.7"
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    implementation (
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion",
            "org.junit.platform:junit-platform-suite:$junitPlatformVersion",
            platform("org.junit:junit-bom:$junitVersion"),
            "org.junit.vintage:junit-vintage-engine:${junitVersion}",

            "com.codeborne:selenide:$selenideVersion",

            "io.cucumber:cucumber-java:$cucumberVersion",
            "io.cucumber:cucumber-junit:$cucumberVersion",
            "io.cucumber:cucumber-junit-platform-engine:$cucumberVersion",
            "io.cucumber:cucumber-core:$cucumberVersion",
            platform("io.cucumber:cucumber-bom:$cucumberVersion"),
            "io.cucumber:cucumber-picocontainer:$cucumberVersion",

            "io.qameta.allure:allure-cucumber7-jvm:$allureVersion",
            "io.qameta.allure:allure-junit5:$allureVersion",

            "org.slf4j:slf4j-simple:$slf4jVersion"
    )

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}


test {
    systemProperties = System.properties as Map<String, ?>
    useJUnitPlatform()

    ignoreFailures = true

    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
    systemProperty "cucumber.junit-platform.naming-strategy", "long"
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
//    systemProperty 'browser', System.getProperty('selenide.browser', 'chrome')
//    systemProperty 'mode', System.getProperty('mode', 'LOCAL')
//    systemProperty 'headless', System.getProperty('selenide.headless', 'false')
//    systemProperty 'version', System.getProperty('version', 'latest')

    testLogging {
        // set options for log level LIFECYCLE
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat "FULL"
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
            exceptionFormat "FULL"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }

    testLogging.showStandardStreams = true
}

